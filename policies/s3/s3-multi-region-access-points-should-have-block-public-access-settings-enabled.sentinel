# This control checks whether an Amazon S3 Multi-Region Access Point has block public access settings enabled.

# Imports

import "tfconfig/v2" as tfconfig
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name": "s3-multi-region-access-points-should-have-block-public-access-settings-enabled",
	"message":     "S3 Multi-Region Access Points should have block public access settings enabled. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/s3-controls.html#s3-24 for more details.",
	"resource_aws_s3control_multi_region_access_point": "aws_s3control_multi_region_access_point",
	"public_access_block_checks": [
		"block_public_acls",
		"block_public_policy",
		"ignore_public_acls",
		"restrict_public_buckets",
	],
}

# Functions

is_public_access_block_compliant = func(config) {
	details = maps.get(config, "details", [])
	if details is null or details is [] {
		return false
	}
	for details as detail {
		public_access_block = maps.get(detail, "public_access_block", [])
		if public_access_block is [] or public_access_block is null {
			return false
		}

		for public_access_block as block {
			for const.public_access_block_checks as check {
				if not maps.has(block, check) {
					return false
				}
				if not maps.get(block, check + ".constant_value", false) is true {
					return false
				}
			}
		}
	}
	return true
}

get_compliance_s3_buckets = func(compliance_resources) {
	compliance_s3_buckets = []

	for compliance_resources as res {
		details = maps.get(maps.get(res, "config", {}), "details", [])
		if details is [] or details is null {
			return true
		}
		for details as detail {
			regions = maps.get(detail, "region", [])
			if regions is [] or regions is null {
				return true
			}
			for regions as region {
				for maps.get(region, "bucket.references", []) as bucket_ref {
					if bucket_ref in s3_bucket_address {
						append(compliance_s3_buckets, bucket_ref)
					}
				}
			}
		}
	}
	return compliance_s3_buckets
}

# Variables

resources = tf.config(tfconfig.resources).type(const.resource_aws_s3control_multi_region_access_point).resources
s3_bucket_resources = tf.config(tfconfig.resources).type("aws_s3_bucket").resources

s3_bucket_address = map s3_bucket_resources as res {
	res.address
}

compliance_resources = collection.filter(resources, func(res) {
	config = maps.get(res, "config", {})
	return is_public_access_block_compliant(config)
})

compliance_s3_buckets = get_compliance_s3_buckets(compliance_resources)

violations = collection.filter(s3_bucket_resources, func(res) {
	return res.address not in compliance_s3_buckets
})

print(violations is empty)

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
