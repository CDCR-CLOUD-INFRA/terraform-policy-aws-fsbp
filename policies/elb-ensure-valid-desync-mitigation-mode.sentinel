# This policy checks if resources of type 'aws_elb' has the
# 'desync_mitigation_mode' attribute set to either 'defensive' or
# 'strictest'

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants
const = {
	"policy_name":      "elb-ensure-valid-desync-mitigation-mode",
	"message":          "Classic load balancers should be configured with defensive or strictest desync mitigation mode. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/elb-controls.html#elb-14 for more details.",
	"resource_aws_elb": "aws_elb",
	"defensive":        "defensive",
	"strictest":        "strictest",
}

# Variables

classic_load_balancers = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_elb).resources

invalid_load_balancers = collection.reject(classic_load_balancers, func(res) {
	return maps.get(res, "values.desync_mitigation_mode", const.defensive) in [const.defensive, const.strictest]
})

summary = {
	"policy_name": const.policy_name,
	"violations": map invalid_load_balancers as _, res {
		{
			"address":        res.address,
			"module_address": res.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	invalid_load_balancers is empty
}
