# This control checks whether an Amazon SageMaker AI image has the tag keys specified by the requiredKeyTags parameter.

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                  "sagemaker-images-should-be-tagged",
	"message":                      "SageMaker images should have tags. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/sagemaker-controls.html#sagemaker-7 for more details.",
	"resource_aws_sagemaker_image": "aws_sagemaker_image",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_sagemaker_image).resources
violations = collection.reject(resources, func(res) {
	tags = maps.get(res, "values.tags", {})
	return tags is not null and length(tags) > 0
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
