# This control checks whether an Amazon SageMaker AI app image configuration (AppImageConfig) has the tag keys specified by the requiredKeyTags parameter.

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name": "sagemaker-app-image-config-tagged",
	"message":     "SageMaker app image configurations should be tagged. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/sagemaker-controls.html#sagemaker-6 for more details.",
	"resource_aws_sagemaker_app_image_config": "aws_sagemaker_app_image_config",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_sagemaker_app_image_config).resources
violations = collection.reject(resources, func(res) {
	tags = maps.get(res, "values.tags", {})
	return tags is not null and tags is not {} and length(tags) > 0
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
