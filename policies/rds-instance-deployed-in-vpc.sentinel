# This policy checks if the aws rds instances are publicly accessible

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps
import "json"

# Constants
const = {
	"policy_name":  "rds-instance-deployed-in-vpc",
	"message":      "Attribute 'vpc_security_group_ids' should be present with some values for aws_db_instance resource. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-18 for more details.",
	"rds_resource": "aws_db_instance",
}

# Functions

build_violation_object = func(res, message) {
	return {
		"address":        res.address,
		"module_address": res.module_address,
		"message":        message,
	}
}

# Variables

rds_resources = tf.plan(tfplan.planned_values.resources).type(const.rds_resource).resources
violations = collection.reject(rds_resources, func(res) {
	vpc_security_group_ids_key = "values.vpc_security_group_ids"
	return length(maps.get(res, vpc_security_group_ids_key, [])) >= 1
})

rds_violations = map violations as _, res {
	build_violation_object(res, const.message)
}

summary = {
	"policy_name": const.policy_name,
	"violations":  rds_violations,
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
